Option Explicit


Private Const INFO_NUM As Integer = 3
Private Const INFO_HEADER_AREA As String = "A1:D10"


Private Sub UserForm_Initialize()
    Application.ScreenUpdating = False
    
    Dim wbList As Workbook
    Set wbList = Workbooks.Open("\list.xlsx")
    
    ' 選択肢をセルから配列に取り込み
    Dim cellInfoLeftTop As Range
    Set cellInfoLeftTop = GetSpecificStringCell("所属", wbList.Worksheets(1).Range(INFO_HEADER_AREA)).Offset(1, 0)
    Dim arrayInfoList As Variant
    arrayInfoList = LeftTopCellToTable(cellInfoLeftTop)
    
    Set cellInfoLeftTop = Nothing
    wbList.Close
    Set wbList = Nothing
    
    ' 選択肢をリストボックスにセット
    With Me.lbInfoList
        .ColumnCount = INFO_NUM
        .ColumnWidths = "50;50;100"
        .List() = arrayInfoList
    End With
    
    Application.ScreenUpdating = True
End Sub

Private Sub cbSetData_Click()
    Dim index As Integer
    index = Me.lbInfoList.ListIndex
    
    If index = -1 Then
        Call Unload(frmInfoList)
        Exit Sub
    End If
    
    Application.ScreenUpdating = False
    
    Dim cellOutput As Range
    Set cellOutput = Selection
    
    Dim i As Integer
    
    ' 選択したデータをセルに書き込み
    For i = 0 To INFO_NUM - 1
        cellOutput.Offset(i, 0).Value = Me.lbInfoList.List(index, i)
    Next i
    
    Application.ScreenUpdating = True
    
    ' 書いたらフォームは閉じる
    Call Unload(frmInfoList)
End Sub

Function GetSpecificStringCell(ByVal target As String, ByRef searchArea As Range) As Range
    Dim c As Variant
    
    For Each c In searchArea
        If c.Value = target Then
            Set GetSpecificStringCell = c
            Exit Function
        End If
    Next
End Function

' テーブルの左上のセル(引数)からテーブル右下までのセル範囲を返す
Function LeftTopCellToTable(ByRef cellLeftTop As Range) As Range
    Dim cellLeftBottom As Range
    If cellLeftTop.Offset(1, 0).Value <> "" Then
        Set cellLeftBottom = cellLeftTop.End(xlDown)
    Else
        Set cellLeftBottom = cellLeftTop
    End If
    
    Dim cellRightBottom As Range
    If cellLeftBottom.Offset(0, 1).Value <> "" Then
        Set cellRightBottom = cellLeftBottom.End(xlToRight)
    Else
        Set cellRightBottom = cellLeftBottom
    End If
    
    Set LeftTopCellToTable = Range(cellLeftTop, cellRightBottom)
End Function
